# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Upstream contributor: Renato Silva <br.renatosilva@gmail.com>

_realname="libiconv"
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="Character encoding conversion library (mingw-w64)"
pkgver=1.18
pkgrel=1
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://www.gnu.org/software/libiconv/"
license=("spdx:LGPL-2.1-or-later")
makedepends=(
  "autotools"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-gperf"
  "${MINGW_PACKAGE_PREFIX}-libtool"
)
source=(
  "https://ftp.gnu.org/pub/gnu/libiconv/${_realname}-${pkgver}.tar.gz"{,.sig}
  "0001-fix-version-symbol.patch"
  "0002-fix-cr-for-awk-in-configure.all.patch"
  "0003-add-cp65001-as-utf8-alias.patch"
  "fix-pointer-buf.patch"
  "iconv.pc"
)
sha256sums=(
  "3b08f5f4f9b4eb82f151a7040bfd6fe6c6fb922efe4b1659c66ea933276965e8"
  "SKIP"
  "2f59dcc4741a1773d1bbbcb6671247f1dcd5efa94c54079f3aab12d553f117a0"
  "89d5c0f666e50a0186cfb142ba7b77e8fe1ac4d65bdfd9ae14ae8d2f0045a87c"
  "cb2b1cca44c9b9bd11c3bd33997c10d22dff263542275324c88f34b59910ef87"
  "6b6e2393840f4dc6067587165777b1a07978f4c05247d7c1010a45ad251bbeeb"
  "56e7ec406bf42eb66b1d972f20d229f7a76ba00c38c7b4403bf348875704baae"
)
validpgpkeys=("9001B85AF9E1B83DF1BDA942F5BE8B267C6A406D") # "Bruno Haible (Open Source Development) <bruno@clisp.org>"

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-fix-version-symbol.patch"
  patch -Np1 -i "${srcdir}/0002-fix-cr-for-awk-in-configure.all.patch"
  patch -Np1 -i "${srcdir}/fix-pointer-buf.patch"
  patch -Np1 -i "${srcdir}/0003-add-cp65001-as-utf8-alias.patch"
  CC="${CC}" make -f Makefile.devel all
}

build() {
  mkdir "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"
  "${srcdir}/${_realname}-${pkgver}/configure" \
    --prefix="${MINGW_PREFIX}" \
    --disable-extra-encodings \
    --disable-nls \
    --enable-relocatable \
    --enable-shared \
    --enable-silent-rules \
    --enable-static
  make -j$(nproc)
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  rm -r "${pkgdir}${MINGW_PREFIX}/bin/"*.exe
  rm -r "${pkgdir}${MINGW_PREFIX}/share"
  install -Dm644 "${srcdir}/iconv.pc" "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/iconv.pc"
  sed -e "s|@PREFIX@|${MINGW_PREFIX}|g" \
      -e "s|@VERSION@|${pkgver}|g" \
      -i "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/iconv.pc"
}
