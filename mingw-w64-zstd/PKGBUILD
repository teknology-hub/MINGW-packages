# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Chocobo1

_realname="zstd"
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="Zstandard - Fast real-time compression algorithm (mingw-w64)"
pkgver=1.5.7
pkgrel=1
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://facebook.github.io/zstd/"
msys2_references=("cpe: cpe:/a:facebook:zstandard")
msys2_repository_url="https://github.com/facebook/zstd"
license=("spdx:BSD-3-Clause OR GPL-2.0-or-later")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
)
source=(
  "https://github.com/facebook/zstd/releases/download/v${pkgver}/${_realname}-${pkgver}.tar.gz"{,.sig}
  "zstd-1.4.0-fileio-mingw.patch"
)
sha256sums=(
  "eb33e51f49a15e023950cd7825ca74a4a2b43db8354825ac24fc1b7ee09e6fa3"
  "SKIP"
  "283e584477a8f70c9bea86a500486dd11dfcb59b22690fd94deb44a3f95a6668"
)
validpgpkeys=("4EF4AC63455FC9F4545D9B7DEF8FE99528B52FFD") # Zstandard Release Signing Key <signing@zstd.net>
noextract=("${_realname}-${pkgver}.tar.gz")

prepare() {
  tar -xf "${srcdir}/${_realname}-${pkgver}.tar.gz" -C "${srcdir}" || true
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/zstd-1.4.0-fileio-mingw.patch"
}

build() {
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  "${MINGW_PREFIX}/bin/cmake" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DBUILD_TESTING=OFF \
    -DZSTD_PROGRAMS_LINK_SHARED=ON \
    -S "${srcdir}/${_realname}-${pkgver}/build/cmake" \
    -B "${srcdir}/build-${MSYSTEM}"
  "${MINGW_PREFIX}/bin/cmake" --build "${srcdir}/build-${MSYSTEM}"
}


package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "${srcdir}/build-${MSYSTEM}"
}
