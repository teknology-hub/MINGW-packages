# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname="libidn2"
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="Implementation of the Stringprep, Punycode and IDNA specifications (mingw-w64)"
pkgver=2.3.8
pkgrel=4
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://www.gnu.org/software/libidn/#libidn2"
msys2_references=("cpe: cpe:/a:gnu:libidn2")
msys2_repository_url="https://gitlab.com/libidn/libidn2"
license=("spdx:GPL-2.0-or-later' 'spdx:LGPL-3.0-or-later")
depends=(
  "${MINGW_PACKAGE_PREFIX}-libiconv"
  "${MINGW_PACKAGE_PREFIX}-libunistring"
)
makedepends=(
  "autotools"
  "gtk-doc"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-libtool"
)
source=("https://ftp.gnu.org/gnu/libidn/${_realname}-${pkgver}.tar.gz"{,.sig})
sha256sums=(
  "f557911bf6171621e1f72ff35f5b1825bb35b52ed45325dcdee931e5d3c0787a"
  "SKIP"
)
validpgpkeys=("B1D2BD1375BECB784CF4F8C4D73CF638C53C06BE") # Simon Josefsson <simon@josefsson.org>

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  AUTOPOINT=true autoreconf -ivf
}

build() {
  mkdir "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"
  "${srcdir}/${_realname}-${pkgver}/configure" \
    --prefix="${MINGW_PREFIX}" \
    --disable-doc \
    --disable-nls \
    --enable-shared \
    --enable-static
  make -j$(nproc)
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}${MINGW_PREFIX}/lib/"*.def || true
}
