# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname="installer"
pkgbase="mingw-w64-python-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-python-${_realname}"
pkgdesc="A low-level library for installing from a Python wheel distribution (mingw-w64)"
pkgver=0.7.0
pkgrel=6
_launcher_commit="8c455825fc1308d34a1a47238a9eb6d56f732142"
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://github.com/pradyunsg/installer"
msys2_references=("purl: pkg:pypi/installer")
license=("spdx:MIT")
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=(
  "git"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-llvm-tools"
  "${MINGW_PACKAGE_PREFIX}-python-flit-core"
)
source=(
  "https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
  "simple_launcher"::"git+https://bitbucket.org/vinay.sajip/simple_launcher.git#commit=${_launcher_commit}"
  "001-launcher-relative.patch"
  "002-launcher-stderr-missing.patch"
  "003-launcher-secure-api-shim.patch"
  "004-zero-reservered-fields-in-startupinfo.patch"
)
sha256sums=(
  "a26d3e3116289bb08216e0d0f7d925fcef0b0194eedfa0c944bcaaa106c4b631"
  "0dc8e2a1e21943a2c81ef4fd22c058a561626b2ba0efa38784df1f4a28a71e8b"
  "a03dc0cb9272a233831ab41ed55d9a64196ab10a750d0517c67745cc5214eb79"
  "e95b68c14bffddd6dd859bdb2c8feb733bbcd5c9d4a37105df4196bfa01923c5"
  "0b6ab782f1550ea0e52a8869cf9c78c405a9ed9dda1e9d9eb60a4126787db208"
  "ccb341afa03809257d0c9c6c5d323b9998d21ff972789eb8c1734d5bc2a7fbff"
)

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/001-launcher-relative.patch"
  cd "${srcdir}/simple_launcher"
  patch -Np1 -i "${srcdir}/002-launcher-stderr-missing.patch"
  patch -Np1 -i "${srcdir}/003-launcher-secure-api-shim.patch"
  patch -Np1 -i "${srcdir}/004-zero-reservered-fields-in-startupinfo.patch"
}

build() {
  cd "${srcdir}/simple_launcher"
  "${MINGW_PREFIX}/bin/windres" --input launcher.rc --output resources.o --output-format=coff
  "${MINGW_PREFIX}/bin/cc" ${CFLAGS} ${CPPFLAGS} -D_CONSOLE -o t64.exe launcher.c resources.o ${LDFLAGS} -lshlwapi
  "${MINGW_PREFIX}/bin/cc" ${CFLAGS} ${CPPFLAGS} -mwindows -o w64.exe launcher.c resources.o ${LDFLAGS} -lshlwapi
  cd "${srcdir}/${_realname}-${pkgver}"
  rm src/installer/_scripts/*.exe
  cp "${srcdir}/simple_launcher/"*.exe src/installer/_scripts/
  "${MINGW_PREFIX}/bin/python" -m flit_core.wheel
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  PYTHONPATH="${srcdir}/${_realname}-${pkgver}/src" \
  "${MINGW_PREFIX}/bin/python" -m installer \
    --prefix="${MINGW_PREFIX}" \
    --destdir="${pkgdir}" \
    dist/*.whl
}
