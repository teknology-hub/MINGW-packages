# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname="mpdecimal"
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="Package for correctly-rounded arbitrary precision decimal floating point arithmetic (mingw-w64)"
pkgver=4.0.1
pkgrel=1
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://www.bytereef.org/mpdecimal/index.html"
license=("spdx:BSD-2-Clause")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=(
  "autotools"
  "${MINGW_PACKAGE_PREFIX}-cc"
)
source=("http://www.bytereef.org/software/mpdecimal/releases/${_realname}-${pkgver}.tar.gz")
sha256sums=("96d33abb4bb0070c7be0fed4246cd38416188325f820468214471938545b1ac8")

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  autoreconf -fiv
}

build() {
  mkdir "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"
  "${srcdir}/${_realname}-${pkgver}/configure" \
    --prefix="${MINGW_PREFIX}" \
    --disable-doc
  make -j$(nproc)
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
