# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: @naveen521kk on Github Naveen M K <naveen521kk@gmail.com>

_realname="build"
pkgbase="mingw-w64-python-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-python-${_realname}"
pkgdesc="A simple, correct Python build frontend (mingw-w64)"
pkgver=1.4.0
pkgrel=2
arch=("any")
mingw_arch=("tek-x86_64")
url="https://github.com/pypa/build"
msys2_references=("purl: pkg:pypi/build")
license=("spdx:MIT")
depends=(
  "${MINGW_PACKAGE_PREFIX}-python-packaging"
  "${MINGW_PACKAGE_PREFIX}-python-pyproject-hooks"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-flit-core"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
)
source=(
  "https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
  "0001-optional-colorama.patch"
)
sha256sums=(
  "f1b91b925aa322be454f8330c6fb48b465da993d1e7e7e6fa35027ec49f3c936"
  "b61ec061e373053e7e53e7c7525acaa9545adc01e532e2e8db16d7cc71bb365e"
)

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-optional-colorama.patch"
}

build() {
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"
  "${MINGW_PREFIX}/bin/python" -m flit_core.wheel
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  "${MINGW_PREFIX}/bin/python" -m installer \
    --prefix="${MINGW_PREFIX}" \
    --destdir="${pkgdir}" \
    dist/*.whl
}
