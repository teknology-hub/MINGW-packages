# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: @naveen521kk on Github Naveen M K <naveen521kk@gmail.com>

_realname="build"
pkgbase="mingw-w64-python-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-python-${_realname}"
pkgdesc="A simple, correct Python build frontend (mingw-w64)"
pkgver=1.3.0
pkgrel=1
arch=("any")
mingw_arch=("tek-x86_64")
url="https://github.com/pypa/build"
msys2_references=("purl: pkg:pypi/build")
license=("spdx:MIT")
depends=(
  "${MINGW_PACKAGE_PREFIX}-python-packaging"
  "${MINGW_PACKAGE_PREFIX}-python-pyproject-hooks"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-flit-core"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
)
source=(
  "https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
  "0001-optional-colorama.patch"
)
sha256sums=(
  "698edd0ea270bde950f53aed21f3a0135672206f3911e0176261a31e0e07b397"
  "2ad33a8d2d16bb09896dfe1020593bbe664e13d8ffd20cd7bc6f68d31d47e0e9"
)

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-optional-colorama.patch"
}

build() {
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"
  "${MINGW_PREFIX}/bin/python" -m flit_core.wheel
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  "${MINGW_PREFIX}/bin/python" -m installer \
    --prefix="${MINGW_PREFIX}" \
    --destdir="${pkgdir}" \
    dist/*.whl
}
