# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Drew Waranis <drew@waran.is>

_realname="jsoncpp"
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="A C++ library for interacting with JSON (mingw-w64)"
pkgver=1.9.6
pkgrel=3
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://github.com/open-source-parsers/jsoncpp"
msys2_references=("cpe: cpe:2.3:a:jsoncpp_project:jsoncpp")
license=("spdx:MIT")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
)
source=(
  "https://github.com/open-source-parsers/jsoncpp/archive/${pkgver}/${_realname}-${pkgver}.tar.gz"
  "0001-static-lib-name.patch"
)
sha256sums=(
  "f93b6dd7ce796b13d02c108bc9f79812245a82e577581c4c9aabe57075c90ea2"
  "692d7ae7f102644aaf7a41f1f9eafd7b3eca45febd6bde8675afdabca85adb4f"
)

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-static-lib-name.patch"
}

build() {
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  "${MINGW_PREFIX}/bin/cmake" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_DLL_NAME_WITH_SOVERSION=ON \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DBUILD_OBJECT_LIBS=OFF \
    -DJSONCPP_WITH_TESTS=OFF \
    -S "${srcdir}/${_realname}-${pkgver}" \
    -B "${srcdir}/build-${MSYSTEM}"
  "${MINGW_PREFIX}/bin/cmake" --build "${srcdir}/build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "${srcdir}/build-${MSYSTEM}"
}
