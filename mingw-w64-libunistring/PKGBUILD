# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname="libunistring"
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="Library for manipulating Unicode strings and C strings. (mingw-w64)"
pkgver=1.3
pkgrel=1
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://www.gnu.org/software/libunistring"
msys2_references=("cpe: cpe:2.3:a:gnu:libunistring")
msys2_repository_url="https://git.savannah.gnu.org/gitweb/?p=libunistring.git"
license=("spdx:LGPL-3.0-or-later OR GPL-3.0-or-later")
depends=("${MINGW_PACKAGE_PREFIX}-libiconv")
makedepends=(
  "autotools"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-libtool"
)
source=(
  "https://ftp.gnu.org/gnu/libunistring/${_realname}-${pkgver}.tar.gz"{,.sig}
  "0001-disable-docs-and-tests.patch"
)
sha256sums=(
  "8ea8ccf86c09dd801c8cac19878e804e54f707cf69884371130d20bde68386b7"
  "SKIP"
  "71a45bf07b90ad3325700797551772ee040cea0b534a9f75f2111f05240620ef"
)
validpgpkeys=('9001B85AF9E1B83DF1BDA942F5BE8B267C6A406D') # Bruno Haible (Open Source Development) <bruno@clisp.org>

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-disable-docs-and-tests.patch"
  autoreconf -fiv
}

build() {
  mkdir "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"
  # Disable LTO as it causes some public functions to not be exported
  CFLAGS="${CFLAGS//-flto=thin/}"
  LDFLAGS="${LDFLAGS//-flto=thin/}"
  "${srcdir}/${_realname}-${pkgver}/configure" \
    --prefix="${MINGW_PREFIX}" \
    --enable-threads=windows
  make -j$(nproc)
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
}
