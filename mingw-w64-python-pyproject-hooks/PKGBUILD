# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_pyname="pyproject_hooks"
_realname="pyproject-hooks"
pkgbase="mingw-w64-python-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-python-${_realname}"
pkgdesc="A low-level library for calling build-backends in pyproject.toml-based project (mingw-w64)"
pkgver=1.2.0
pkgrel=2
arch=("any")
mingw_arch=("tek-x86_64")
url="https://pyproject-hooks.readthedocs.io/"
msys2_references=(
  "archlinux: python-pyproject-hooks"
  "purl: pkg:pypi/pyproject-hooks"
)
msys2_repository_url="https://github.com/pypa/pyproject-hooks"
license=("spdx:MIT")
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-flit-core"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
)
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=("1e859bd5c40fae9448642dd871adf459e5e2084186e8d2c2a79a824c970da1f8")

build() {
  cp -r "${srcdir}/${_pyname}-${pkgver}" "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"
  "${MINGW_PREFIX}/bin/python" -m flit_core.wheel
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  "${MINGW_PREFIX}/bin/python" -m installer \
    --prefix="${MINGW_PREFIX}" \
    --destdir="${pkgdir}" \
    dist/*.whl
}
