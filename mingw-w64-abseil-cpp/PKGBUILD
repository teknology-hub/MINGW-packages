# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Michał Łukowski <michal.lukowski@gmail.com>

_realname="abseil-cpp"
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="Abseil Common Libraries (C++) from Google (mingw-w64)"
pkgver=20250814.1
pkgrel=1
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://abseil.io"
msys2_references=('archlinux: abseil-cpp')
msys2_repository_url="https://github.com/abseil/abseil-cpp"
license=('spdx:Apache-2.0')
depends=(
  "${MINGW_PACKAGE_PREFIX}-cc-libs"
  "${MINGW_PACKAGE_PREFIX}-winpthreads"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
)
source=(
  "https://github.com/abseil/abseil-cpp/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz"
  "0001-abseil-Fix-compiler-warnings.patch"
  "0002-abseil-Remove-librt-library.patch"
  "0003-fix-linking-abseil_dll.patch"
)
sha256sums=(
  "1692f77d1739bacf3f94337188b78583cf09bab7e420d2dc6c5605a4f86785a1"
  "30fb7526d1c89f722dd0ce0c467e034370140fbf07a1ad4f91df301c7f442551"
  "8946282db6a225b385729deb345b57e3ee75f4e859dbbc49ab7bc5604d71192f"
  "67ee0328f1aa8e3ad24231b2e6ef6a78963218e087cc6a7230e2d4e61b27a741"
)

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-abseil-Fix-compiler-warnings.patch"
  patch -Np1 -i "${srcdir}/0002-abseil-Remove-librt-library.patch"
  patch -Np1 -i "${srcdir}/0003-fix-linking-abseil_dll.patch"
}

build() {
  local _common_config=(
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_CXX_STANDARD=23
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}"
    -DABSL_PROPAGATE_CXX_STD=ON
    -S "${srcdir}/${_realname}-${pkgver}"
  )
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  "${MINGW_PREFIX}/bin/cmake" \
    -DBUILD_SHARED_LIBS=OFF \
    "${_common_config[@]}" \
    -B "${srcdir}/build-${MSYSTEM}-static"
  "${MINGW_PREFIX}/bin/cmake" --build "${srcdir}/build-${MSYSTEM}-static"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  "${MINGW_PREFIX}/bin/cmake" \
    -DBUILD_SHARED_LIBS=ON \
    "${_common_config[@]}" \
    -B "${srcdir}/build-${MSYSTEM}"
  "${MINGW_PREFIX}/bin/cmake" --build "${srcdir}/build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "${srcdir}/build-${MSYSTEM}-static"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "${srcdir}/build-${MSYSTEM}"
}
