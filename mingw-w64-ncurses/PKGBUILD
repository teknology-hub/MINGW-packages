# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname="ncurses"
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="System V Release 4.0 curses emulation library (mingw-w64)"
_base_ver=6.5
_date_rev=20250927
pkgver="${_base_ver}.${_date_rev}"
pkgrel=2
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://www.gnu.org/software/ncurses/"
msys2_references=(
  "cpe: cpe:/a:gnu:ncurses"
  "cpe: cpe:/a:ncurses_project:ncurses"
)
license=("spdx:MIT")
depends=("${MINGW_PACKAGE_PREFIX}-cc-libs")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc")
source=(
  "https://invisible-mirror.net/archives/ncurses/current/${_realname}-${_base_ver}-${_date_rev}.tgz"{,.asc}
  "002-ncurses-config-win-paths.patch"
  "ncurses-6.3-cflags-private.patch"
  "ncurses-6.3-pkgconfig.patch"
)
sha256sums=(
  "ad35dc77453766b5c4042cda80fc95b3ca84e25eb8f99aa571f13773c38b47fa"
  "SKIP"
  "5367d8f49aff92884b9daa014502df13e1812f1b7ee1b3a3cb18139f10039408"
  "3107029dfb807e338d34641d78329cd6725c58e6b873352621f4b9611a8380bf"
  "b8544a607dfbeffaba2b087f03b57ed1fa81286afca25df65f61b04b5f3b3738"
)
validpgpkeys=("19882D92DDA4C400C22C0D56CC2AF4472167BE03") # "Thomas E. Dickey (self-signed w/o SHA1) <dickey@invisible-island.net>"

prepare() {
  cd "${srcdir}/${_realname}-${_base_ver}-${_date_rev}"
  patch -Np1 -i "${srcdir}/002-ncurses-config-win-paths.patch"
  patch -Np1 -i "${srcdir}/ncurses-6.3-pkgconfig.patch"
  patch -Np1 -i "${srcdir}/ncurses-6.3-cflags-private.patch"
}

build() {
  mkdir "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"
  CFLAGS+=" -D__USE_MINGW_ACCESS"
  cf_cv_func_nanosleep=no
  "${srcdir}/${_realname}-${_base_ver}-${_date_rev}/configure" \
    --prefix=${MINGW_PREFIX} \
    --program-prefix="${MINGW_CHOST}-" \
    --disable-home-terminfo \
    --disable-symlinks \
    --enable-colorfgbg \
    --enable-database \
    --enable-ext-colors \
    --enable-ext-mouse \
    --enable-interop \
    --enable-pc-files \
    --enable-sp-funcs \
    --enable-term-driver \
    --enable-widec \
    --with-shared \
    --without-ada \
    --without-manpages \
    --without-pthread \
    --without-tests \
    --with-pkg-config-libdir="${MINGW_PREFIX}/lib/pkgconfig"
  make -j$(nproc)
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  cp -r "${pkgdir}${MINGW_PREFIX}/include/ncursesw" "${pkgdir}${MINGW_PREFIX}/include/ncurses"
  cp "${pkgdir}${MINGW_PREFIX}/lib/libncursesw.a" "${pkgdir}${MINGW_PREFIX}/lib/libncurses.a"
  for pcfile in "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/"*.pc; do
    sed -s "s|${MINGW_PREFIX}/include|\${includedir}|g" -i "${pcfile}"
    sed -s "s|${MINGW_PREFIX}/lib|\${libdir}|g" -i "${pcfile}"
  done
}
