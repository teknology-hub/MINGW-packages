# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Alexey Pavlov <alexpux@gmail.com>
# Upstream contributor: Ray Donnelly <mingw.android@gmail.com>

_realname="setuptools"
pkgbase="mingw-w64-python-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-python-${_realname}"
pkgdesc="Easily download, build, install, upgrade, and uninstall Python packages (mingw-w64)"
pkgver=80.9.0
pkgrel=2
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://github.com/pypa/setuptools"
msys2_references=(
  "cpe: cpe:/a:python:setuptools"
  "purl: pkg:pypi/setuptools"
)
license=("spdx:MIT")
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
)
source=(
  "https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz"
  "0002-Allow-usr-bin-env-in-script.patch"
  "0003-MinGW-w64-Look-in-same-dir-as-script-for-exe.patch"
  "0005-execv-warning.patch"
  "0006-fix-tests-path.patch"
  "0007-windows-arm64.patch"
  "0009-launcher-revert-symlink-resolve.patch"
  "0010-support-py_limited_api.patch"
  "0011-add-support-for-windmc.patch"
)
sha256sums=(
  "f36b47402ecde768dbfafc46e8e4207b4360c654f1f3bb84475f0a28628fb19c"
  "fa54581e3dddb9f4edd332dedbc101f48939a9ca5878e13cf9cf9b3408bc8c22"
  "39503256652c7c23ce07e26539e8123d269eb5c09f5d9b07e5784b2d7fb8c96f"
  "a356b0663f67a296624d1b178b42437b380b48a4bbe05a560d3bf29e93a4c623"
  "dbdd96a7ead797b2db9f02dfe19ce853d3775337ccf8fd836377fe3c2a9d1c24"
  "fbf6903e88c0fe0b307365db997505c3d9f07184792aea1442616a3d97f037d9"
  "391d682a8b8832d7e4be3ac0c3327cfe3c8e520f16e25b43d4441df8036bf147"
  "ccb276f9d2d26587d29ecfd4d47a3d4e96fb58738cfaf3a600a8b942c79fc439"
  "5732980fe5d94f3371ccd0609b160e3b74f88ab71829d0e8b37f6039f4508b79"
)

prepare() {
  cd "${srcdir}/setuptools-${pkgver}"
  patch -Np1 -i "${srcdir}/0002-Allow-usr-bin-env-in-script.patch"
  patch -Np1 -i "${srcdir}/0003-MinGW-w64-Look-in-same-dir-as-script-for-exe.patch"
  patch -Np1 -i "${srcdir}/0005-execv-warning.patch"
  patch -Np1 -i "${srcdir}/0006-fix-tests-path.patch"
  patch -Np1 -i "${srcdir}/0007-windows-arm64.patch"
  patch -Rp1 -i "${srcdir}/0009-launcher-revert-symlink-resolve.patch"
  patch -Np1 -i "${srcdir}/0010-support-py_limited_api.patch"
  patch -Np1 -i "${srcdir}/0011-add-support-for-windmc.patch"
  mv "setuptools/command/launcher manifest.xml" setuptools/
}

build() {
  cp -r "${srcdir}/${_realname}-${pkgver}" "${srcdir}/build-${MSYSTEM}"
  cd "${srcdir}/build-${MSYSTEM}"
  rm -f setuptools/{gui,cli}{-32,-64,-arm64}.exe
  "${MINGW_PREFIX}/bin/cc" ${CFLAGS} ${CPPFLAGS} -DGUI=0 -s -o setuptools/cli-64.exe launcher.c ${LDFLAGS}
  "${MINGW_PREFIX}/bin/cc" ${CFLAGS} ${CPPFLAGS} -DGUI=1 -mwindows -s -o setuptools/gui-64.exe launcher.c ${LDFLAGS}
  PYTHONPATH="${srcdir}/build-${MSYSTEM}" \
  "${MINGW_PREFIX}/bin/python" -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  "${MINGW_PREFIX}/bin/python" -m installer \
    --prefix="${MINGW_PREFIX}" \
    --destdir="${pkgdir}" \
    dist/*.whl
}
