# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Alexey Pavlov <alexpux@gmail.com>
# Upstream contributor: Martell Malone <martellmalone@gmail.com>

_realname="libuv"
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="Cross-platform asynchronous I/O (mingw-w64)"
pkgver=1.51.0
pkgrel=1
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://libuv.org/"
msys2_documentation_url="https://docs.libuv.org/"
msys2_references=(
  "archlinux: libuv"
  "cpe: cpe:/a:libuv:libuv"
  "cpe: cpe:/a:libuv_project:libuv"
)
msys2_repository_url="https://github.com/libuv/libuv/"
license=("spdx:MIT")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
)
source=("https://dist.libuv.org/dist/v${pkgver}/${_realname}-v${pkgver}.tar.gz"{,.sign})
sha256sums=(
  "5f0557b90b1106de71951a3c3931de5e0430d78da1d9a10287ebc7a3f78ef8eb"
  "SKIP"
)
validpgpkeys=(
  "CFBB9CA9A5BEAFD70E2B3C5A79A67C55A3679C8B" # Jameson Nash <vtjnash@gmail.com>
  "612F0EAD9401622379DF4402F28C3C8DA33C03BE" # Santiago Gimeno (sgimeno) <santiago.gimeno@gmail.com>
)

build() {
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  "${MINGW_PREFIX}/bin/cmake" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DLIBUV_BUILD_TESTS=OFF \
    -S "${srcdir}/${_realname}-v${pkgver}" \
    -B "${srcdir}/build-${MSYSTEM}"
  "${MINGW_PREFIX}/bin/cmake" --build "${srcdir}/build-${MSYSTEM}"
}

package() {
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "${srcdir}/build-${MSYSTEM}"
  rm "${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/libuv-static.pc"
  rm -r "${pkgdir}${MINGW_PREFIX}/share"
}
