# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Alexey Pavlov <alexpux@gmail.com>
# Upstream contributor: Renato Silva <br.renatosilva@gmail.com>

_realname="libxml2"
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="XML parsing library, version 2 (mingw-w64)"
pkgver=2.15.1
pkgrel=2
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://gitlab.gnome.org/GNOME/libxml2/-/wikis/home"
msys2_references=("cpe: cpe:/a:xmlsoft:libxml2")
msys2_repository_url="https://gitlab.gnome.org/GNOME/libxml2"
license=("spdx:MIT")
depends=(
  "${MINGW_PACKAGE_PREFIX}-libiconv"
  "${MINGW_PACKAGE_PREFIX}-zlib"
)
makedepends=(
  "autotools"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-libtool"
)
source=(
  "https://download.gnome.org/sources/libxml2/${pkgver%.*}/${_realname}-${pkgver}.tar.xz"
  "0001-do-not-export-DllMain.patch"
  "0029-xml2-config-win-paths.patch"
  "0031-apply-msvc-relocation.patch"
  "0032-cmake-relocation.patch"
)
sha256sums=(
  "c008bac08fd5c7b4a87f7b8a71f283fa581d80d80ff8d2efd3b26224c39bc54c"
  "baad28b724738464cebf1a4e3320111b848fd7b14619e5c0134784ef7ad2aa3a"
  "278b4531da3d2aabda080c412c5122b471202dd6df67768b38bb0c31c7a0e508"
  "bcb2565878e524df3e3bcd1631dbcefc205e9b435a2e537c1810996b97c4dd4a"
  "d2bd68da35aa68197f1b0ebed2c01685a7f4c780430840784a861a540d05b130"
)

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-do-not-export-DllMain.patch"
  patch -Np1 -i "${srcdir}/0029-xml2-config-win-paths.patch"
  patch -Np1 -i "${srcdir}/0031-apply-msvc-relocation.patch"
  patch -Np1 -i "${srcdir}/0032-cmake-relocation.patch"
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"
  # Disable LTO as it breaks executables
  CFLAGS="${CFLAGS//-flto=thin/}" \
  LDFLAGS="${LDFLAGS//-flto=thin/}" \
  "${srcdir}/${_realname}-${pkgver}/configure" \
    --prefix="${MINGW_PREFIX}" \
    --enable-shared \
    --enable-static \
    --with-threads=win32 \
    --with-zlib \
    --without-python
  make -j$(nproc)
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install
  local PREFIX_DEPS="$(cygpath -am "${MINGW_PREFIX}")"
  sed -s "s|${PREFIX_DEPS}\/lib|\${libdir}|g" -i "${pkgdir}"${MINGW_PREFIX}/bin/xml2-config
  sed -s "s|${PREFIX_DEPS}\/lib|\${libdir}|g" -i "${pkgdir}"${MINGW_PREFIX}/lib/pkgconfig/libxml-2.0.pc
}
