# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Andrea Zagli <andrea.zagli.free@gmail.com>

_realname="meson"
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgdesc="High-productivity build system (mingw-w64)"
pkgver=1.10.0
pkgrel=3
arch=("any")
mingw_arch=("tek-x86_64")
url="https://mesonbuild.com/"
msys2_references=(
  "archlinux: meson"
  "purl: pkg:pypi/meson"
)
msys2_repository_url="https://github.com/mesonbuild/meson"
license=("spdx:Apache-2.0")
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
depends=(
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
  "${MINGW_PACKAGE_PREFIX}-python"
)
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python-build"
  "${MINGW_PACKAGE_PREFIX}-python-installer"
  "${MINGW_PACKAGE_PREFIX}-python-setuptools"
)
source=(
  "https://github.com/mesonbuild/meson/releases/download/${pkgver}/${_realname}-${pkgver}.tar.gz"
  "color-term.patch"
  "0002-Default-to-sys.prefix-as-the-default-prefix.patch"
  "0004-fix-gtk-doc.patch"
  "https://github.com/mesonbuild/meson/pull/15387.patch"
  "0005-rust-linker-fix.patch"
)
sha256sums=(
  "8071860c1f46a75ea34801490fd1c445c9d75147a65508cd3a10366a7006cc1c"
  "5805aed0a117536eb16dd8eef978c6be57c2471b655ede63e25517c28b4f4cf0"
  "032b38f0b2765dc88e1fcb34e27b69b611e07c869c13c6e703bcf182e586fccb"
  "bc43a2695498885afa08f2aeb927d6958741aa1a9fc66850de1a6c79fdf59241"
  "dda29950cd44fa947b2eecea33d764b8c276e3cbf58897780128d3b551fe7209"
  "de7a8b42fad5431f6111659a6bba636e3e93fe1627d7ed4c3e2e93ee10b7b674"
)

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/color-term.patch"
  patch -Np1 -i "${srcdir}/0002-Default-to-sys.prefix-as-the-default-prefix.patch"
  patch -Np1 -i "${srcdir}/0004-fix-gtk-doc.patch"
  patch -Np1 -i "${srcdir}/15387.patch"
  patch -Np1 -i "${srcdir}/0005-rust-linker-fix.patch"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  "${MINGW_PREFIX}/bin/python" -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  MSYS2_ARG_CONV_EXCL="--prefix=" \
  "${MINGW_PREFIX}/bin/python" -m installer \
    --prefix="${MINGW_PREFIX}" \
    --destdir="${pkgdir}" \
    dist/*.whl
}
