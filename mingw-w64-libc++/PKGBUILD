# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream contributor: Mehdi Chinoune <mehdi.chinoune@hotmail.com>

_realname="libc++"
pkgbase="mingw-w64-${_realname}"
pkgname=(
  "${MINGW_PACKAGE_PREFIX}-${_realname}"
  "${MINGW_PACKAGE_PREFIX}-libunwind"
)
pkgver=21.1.8
pkgrel=1
arch=("x86_64")
mingw_arch=("tek-x86_64")
msys2_references=("cpe: cpe:/a:llvm:llvm")
license=("spdx:Apache-2.0 WITH LLVM-exception")
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-compiler-rt"
  "${MINGW_PACKAGE_PREFIX}-python"
)
source=("https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}/llvm-project-${pkgver}.src.tar.xz"{,.sig})
sha256sums=(
  "4633a23617fa31a3ea51242586ea7fb1da7140e426bd62fc164261fe036aa142"
  "SKIP"
)
validpgpkeys=(
  "B6C8F98282B944E3B0D5C2530FC3042E345AD05D" # Hans Wennborg, Google.
  "474E22316ABF4785A88C6E8EA2C794A986419D8A" # Tom Stellard
  "71046D1E9C6656BDD61171873E83BABF4A4F9E85" # Cullen Rhodes
  "FFB3368980F3E6BB5737145A316C56D064CACBA5" # Douglas Yung
  "D574BD5D1D0E98895E3BF90044F2485E45D59042" # Tobias Hieta
)
noextract=("llvm-project-${pkgver}.src.tar.xz")

prepare() {
  tar -xf "${srcdir}/llvm-project-${pkgver}.src.tar.xz" -C "${srcdir}" || true
}

build() {
  CXXFLAGS+=" -D_WIN32_WINNT=0xA00"
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  "${MINGW_PREFIX}/bin/cmake" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_SYSTEM_IGNORE_PATH=/usr/lib \
    -DPython3_EXECUTABLE="${MINGW_PREFIX}/bin/python" \
    -DLLVM_ENABLE_RUNTIMES="libcxx;libcxxabi;libunwind" \
    -DLLVM_HOST_TRIPLE="${CARCH}-w64-windows-gnu" \
    -DLLVM_INCLUDE_TESTS=OFF \
    -DLIBCXX_ENABLE_SHARED=ON \
    -DLIBCXX_ENABLE_STATIC=ON \
    -DLIBCXX_ENABLE_STATIC_ABI_LIBRARY=ON \
    -DLIBCXX_HAS_WIN32_THREAD_API=ON \
    -DLIBCXX_INSTALL_MODULES=ON \
    -DLIBCXX_INCLUDE_BENCHMARKS=OFF \
    -DLIBCXX_USE_COMPILER_RT=ON \
    -DLIBCXXABI_ENABLE_SHARED=OFF \
    -DLIBCXXABI_ENABLE_STATIC=ON \
    -DLIBCXXABI_ENABLE_STATIC_UNWINDER=ON \
    -DLIBCXXABI_HAS_WIN32_THREAD_API=ON \
    -DLIBCXXABI_USE_COMPILER_RT=ON \
    -DLIBCXXABI_USE_LLVM_UNWINDER=ON \
    -DLIBUNWIND_ENABLE_FRAME_APIS=ON \
    -DLIBUNWIND_ENABLE_SHARED=ON \
    -DLIBUNWIND_ENABLE_STATIC=ON \
    -DLIBUNWIND_USE_COMPILER_RT=ON \
    -S "${srcdir}/llvm-project-${pkgver}.src/runtimes" \
    -B "${srcdir}/build-${MSYSTEM}"
  "${MINGW_PREFIX}/bin/cmake" --build "${srcdir}/build-${MSYSTEM}" -- unwind cxxabi cxx cxx_experimental
}

package_libc++() {
  pkgdesc="C++ Standard Library (mingw-w64)"
  url="https://libcxx.llvm.org/"
  depends=("${MINGW_PACKAGE_PREFIX}-libunwind")
  provides=(
    "${MINGW_PACKAGE_PREFIX}-cc-libs"
    "${MINGW_PACKAGE_PREFIX}-gcc-libs"
  )

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --build "${srcdir}/build-${MSYSTEM}" --target install-cxx install-cxxabi
}

package_libunwind() {
  pkgdesc="A new implementation of a stack unwinder for C++ exceptions (mingw-w64)"
  url="https://llvm.org/"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --build "${srcdir}/build-${MSYSTEM}" --target install-unwind
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
