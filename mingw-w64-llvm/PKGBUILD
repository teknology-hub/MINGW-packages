# Maintainer: Nuclearist <nuclearist@teknology-hub.com>
# Upstream maintainer: Martell Malone <martellmalone@gmail.com>
# Upstream maintainer: Alexey Pavlov <alexpux@gmail.com>
# Upstream contributor: Ray Donnelly <mingw.android@gmail.com>
# Upstream contributor: Mateusz Miku≈Ça <mati865@gmail.com>
# Upstream contributor: wirx6 <wirx654@gmail.com>
# Upstream contributor: Yuui Tanabe <yuuitanabe@163.com>
# Upstream contributor: Oscar Fuentes <ofv@wanadoo.es>
# Upstream contributor: Adrian Pop <adrian.pop@liu.se>

_realname="llvm"
pkgbase="mingw-w64-${_realname}"
pkgname=(
  "${MINGW_PACKAGE_PREFIX}-clang"
  "${MINGW_PACKAGE_PREFIX}-clang-libs"
  "${MINGW_PACKAGE_PREFIX}-clang-analyzer"
  "${MINGW_PACKAGE_PREFIX}-clang-tools-extra"
  "${MINGW_PACKAGE_PREFIX}-compiler-rt"
  "${MINGW_PACKAGE_PREFIX}-lld"
  "${MINGW_PACKAGE_PREFIX}-llvm"
  "${MINGW_PACKAGE_PREFIX}-llvm-tools"
  "${MINGW_PACKAGE_PREFIX}-llvm-libs"
)
pkgdesc="C language family frontend for LLVM (mingw-w64)"
pkgver=21.1.7
pkgrel=1
arch=("x86_64")
mingw_arch=("tek-x86_64")
url="https://llvm.org/"
msys2_references=("cpe: cpe:/a:llvm:llvm")
msys2_repository_url="https://github.com/llvm/llvm-project"
license=("spdx:Apache-2.0 WITH LLVM-exception")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-compiler-rt"
  "${MINGW_PACKAGE_PREFIX}-libc++"
  "${MINGW_PACKAGE_PREFIX}-libffi"
  "${MINGW_PACKAGE_PREFIX}-libunwind"
  "${MINGW_PACKAGE_PREFIX}-llvm-tools"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
  "${MINGW_PACKAGE_PREFIX}-python"
)
_url="https://github.com/llvm/llvm-project/releases/download/llvmorg-${pkgver}"
source=(
  "${_url}/clang-${pkgver}.src.tar.xz"{,.sig}
  "${_url}/clang-tools-extra-${pkgver}.src.tar.xz"{,.sig}
  "${_url}/cmake-${pkgver}.src.tar.xz"{,.sig}
  "${_url}/compiler-rt-${pkgver}.src.tar.xz"{,.sig}
  "${_url}/lld-${pkgver}.src.tar.xz"{,.sig}
  "${_url}/llvm-${pkgver}.src.tar.xz"{,.sig}
  "${_url}/runtimes-${pkgver}.src.tar.xz"{,.sig}
  "${_url}/third-party-${pkgver}.src.tar.xz"{,.sig}
  "0001-Fix-GetHostTriple-for-mingw-w64-in-msys.patch"
  "0002-Fix-Findzstd-on-MINGW.patch"
  "0303-ignore-new-bfd-options.patch"
)
sha256sums=(
  "eeb5296f46eacc0a1fe6c47a1af9e8d4eb2a5da7254fd8dbc4fc1f1be74315a1"
  "SKIP"
  "e03c2f904e9f7a9a128a2f57e4c50f12d52e11015e415fa784b496b43ce10159"
  "SKIP"
  "f25ca011e4453ac035e940aa729d482d08eb83a91b4aaf1f230dc9ea28cadfa4"
  "SKIP"
  "4358e4476ab89b6ca9e4174f05f4a2c2c10e4a41c3c7febd25afe96bc27b5758"
  "SKIP"
  "607395fc8a5f4d14eb27b219a279558a172d1e3a34ddd2a3509c12b560f1930a"
  "SKIP"
  "8657ee188b673a3285b991131abe905b4129a18d52d334f9d122463164ecd000"
  "SKIP"
  "7f87971685100bc2bd0fe54042c5140da100b0e37fa12fca888cf1d8d06a8e25"
  "SKIP"
  "5fe212feb27f325fa29530eb38e746a73d18205bf6a36d9800778a94108d2ee5"
  "SKIP"
  "eb03df53671df6627768141b3aaa76abe176a14e5e47911c97bec544387c4aff"
  "d77d47e37e4948f5c01ae8bc05fb4e879b6516132c5a60c7bc820c9cbbb39668"
  "fd0253879cc5e31857f57307e6105e56493358e35d7c4540e0b476be607ab94e"
)
validpgpkeys=(
  "B6C8F98282B944E3B0D5C2530FC3042E345AD05D" # Hans Wennborg, Google.
  "474E22316ABF4785A88C6E8EA2C794A986419D8A" # Tom Stellard
  "71046D1E9C6656BDD61171873E83BABF4A4F9E85" # Cullen Rhodes
  "FFB3368980F3E6BB5737145A316C56D064CACBA5" # Douglas Yung
  "D574BD5D1D0E98895E3BF90044F2485E45D59042" # Tobias Hieta
)
noextract=(
  "clang-${pkgver}.src.tar.xz"
  "llvm-${pkgver}.src.tar.xz"
)

prepare() {
  tar -xf "${srcdir}"/clang-${pkgver}.src.tar.xz -C "${srcdir}" || true
  tar -xf "${srcdir}"/llvm-${pkgver}.src.tar.xz -C "${srcdir}" || true
  for pkg in clang clang-tools-extra cmake compiler-rt lld llvm runtimes third-party; do
    mv "${srcdir}/${pkg}-${pkgver}.src" "${srcdir}/${pkg}"
  done
  cd "${srcdir}/llvm"
  patch -Np1 -i "${srcdir}/0001-Fix-GetHostTriple-for-mingw-w64-in-msys.patch"
  patch -Np1 -i "${srcdir}/0002-Fix-Findzstd-on-MINGW.patch"
  cd "${srcdir}/lld"
  patch -Np1 -i "${srcdir}/0303-ignore-new-bfd-options.patch"
}

build() {
  FFI_INCLUDE_DIR="$("${MINGW_PREFIX}/bin/pkgconf" --cflags --keep-system-cflags libffi)"
  FFI_INCLUDE_DIR=$(echo $FFI_INCLUDE_DIR | sed 's|-I||g')
  local _common_config=(
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}"
    -DCMAKE_SYSTEM_IGNORE_PATH=/usr/lib
    -DPython3_EXECUTABLE="${MINGW_PREFIX}/bin/python"
    -DCLANG_DEFAULT_CXX_STDLIB=libc++
    -DCLANG_DEFAULT_LINKER=lld
    -DCLANG_DEFAULT_RTLIB=compiler-rt
    -DCLANG_DEFAULT_UNWINDLIB=libunwind
    -DCOMPILER_RT_BUILD_SANITIZERS=OFF
    -DFFI_INCLUDE_DIR="${FFI_INCLUDE_DIR}"
    -DLLVM_BUILD_LLVM_DYLIB=ON
    -DLLVM_BUILD_STATIC=OFF
    -DLLVM_ENABLE_ASSERTIONS=OFF
    -DLLVM_ENABLE_FFI=ON
    -DLLVM_ENABLE_LTO=Thin
    -DLLVM_ENABLE_THREADS=ON
    -DLLVM_HOST_TRIPLE="${CARCH}-w64-windows-gnu"
    -DLLVM_INCLUDE_BENCHMARKS=OFF
    -DLLVM_INCLUDE_EXAMPLES=OFF
    -DLLVM_INCLUDE_TESTS=OFF
    -DLLVM_LINK_LLVM_DYLIB=ON
  )
  "${MINGW_PREFIX}/bin/cmake" \
    -DCOMPILER_RT_BUILD_PROFILE=OFF \
    -DDEFAULT_SYSROOT="${MINGW_PREFIX}" \
    -DLLVM_BUILD_INSTRUMENTED=IR \
    -DLLVM_ENABLE_PROJECTS="clang" \
    -DLLVM_ENABLE_RUNTIMES="compiler-rt" \
    -DLLVM_TARGETS_TO_BUILD=Native \
    "${_common_config[@]}" \
    -S "${srcdir}/llvm" \
    -B "${srcdir}/build-${MSYSTEM}-instrument"
  "${MINGW_PREFIX}/bin/cmake" --build "${srcdir}/build-${MSYSTEM}-instrument" --target compiler-rt --target clang
  "${MINGW_PREFIX}/bin/cmake" \
    -DCMAKE_C_COMPILER="${srcdir}/build-${MSYSTEM}-instrument/bin/clang.exe" \
    -DCMAKE_CXX_COMPILER="${srcdir}/build-${MSYSTEM}-instrument/bin/clang++.exe" \
    -DLLVM_ENABLE_PROJECTS="clang" \
    -DLLVM_TARGETS_TO_BUILD=Native \
    "${_common_config[@]}" \
    -S "${srcdir}/llvm" \
    -B "${srcdir}/build-${MSYSTEM}-train"
  rm "${srcdir}/build-${MSYSTEM}-instrument/profiles/"*.profraw || true
  "${MINGW_PREFIX}/bin/cmake" --build "${srcdir}/build-${MSYSTEM}-train" --target clangSema
  "${MINGW_PREFIX}/bin/llvm-profdata" merge -output="${srcdir}/profdata" "${srcdir}/build-${MSYSTEM}-instrument/profiles/"*.profraw
  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  "${MINGW_PREFIX}/bin/cmake" \
    -DCMAKE_SYSROOT="${MINGW_PREFIX}" \
    -DCLANG_LINKS_TO_CREATE="clang++;clang-cpp;as;c++;cc;cpp;${MINGW_CHOST}-cc;${MINGW_CHOST}-c++;${MINGW_CHOST}-clang;${MINGW_CHOST}-clang++" \
    -DCOMPILER_RT_USE_BUILTINS_LIBRARY=ON \
    -DLIBCLANG_BUILD_STATIC=ON \
    -DLLD_DEFAULT_LD_LLD_IS_MINGW=ON \
    -DLLVM_ENABLE_PROJECTS="clang;clang-tools-extra;lld" \
    -DLLVM_ENABLE_RTTI=ON \
    -DLLVM_ENABLE_RUNTIMES="compiler-rt" \
    -DLLVM_INSTALL_BINUTILS_SYMLINKS=ON \
    -DLLVM_INSTALL_UTILS=ON \
    -DLLVM_PROFDATA_FILE="${srcdir}/profdata" \
    -DLLVM_TARGETS_TO_BUILD="X86" \
    "${_common_config[@]}" \
    -S "${srcdir}/llvm" \
    -B "${srcdir}/build-${MSYSTEM}"
  "${MINGW_PREFIX}/bin/cmake" --build "${srcdir}/build-${MSYSTEM}"
}

package_clang() {
  pkgdesc="C language family frontend for LLVM (mingw-w64)"
  url="https://clang.llvm.org/"
  groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
  depends=(
    "${MINGW_PACKAGE_PREFIX}-clang-libs=${pkgver}"
    "${MINGW_PACKAGE_PREFIX}-compiler-rt=${pkgver}"
    "${MINGW_PACKAGE_PREFIX}-crt"
    "${MINGW_PACKAGE_PREFIX}-headers"
    "${MINGW_PACKAGE_PREFIX}-lld=${pkgver}"
    "${MINGW_PACKAGE_PREFIX}-llvm-tools=${pkgver}"
    "${MINGW_PACKAGE_PREFIX}-winpthreads"
  )
  provides=("${MINGW_PACKAGE_PREFIX}-cc")

  sed -i.orig '/\(extra\|scan-build\|scan-build-py\|scan-view\)\/cmake_install.cmake/d' "${srcdir}/build-${MSYSTEM}/tools/clang/tools/cmake_install.cmake"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "${srcdir}/build-${MSYSTEM}/tools/clang"
  mkdir -p "${srcdir}/clang-libs/${MINGW_PREFIX}/bin"
  mv -f "${pkgdir}${MINGW_PREFIX}/bin/"libclang{,-cpp}.dll "${srcdir}/clang-libs/${MINGW_PREFIX}/bin/"
}

package_clang-libs() {
  pkgdesc="Clang runtime libraries (mingw-w64)"
  url="https://clang.llvm.org/"
  depends=(
    "${MINGW_PACKAGE_PREFIX}-cc-libs"
    "${MINGW_PACKAGE_PREFIX}-llvm-libs=${pkgver}"
  )

  cp -r "${srcdir}/clang-libs/${MINGW_PREFIX}" "${pkgdir}${MINGW_PREFIX}"
}

package_clang-analyzer() {
  pkgdesc="A source code analysis framework (mingw-w64)"
  url="https://clang-analyzer.llvm.org/"
  depends=("${MINGW_PACKAGE_PREFIX}-clang=${pkgver}")

  for analyzer in scan-build scan-build-py scan-view; do
    DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "${srcdir}/build-${MSYSTEM}/tools/clang/tools/${analyzer}"
  done
}

package_clang-tools-extra() {
  pkgdesc="Extra tools built using Clang's tooling APIs (mingw-w64)"
  url="https://clang.llvm.org/"
  depends=("${MINGW_PACKAGE_PREFIX}-clang=${pkgver}")

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "${srcdir}/build-${MSYSTEM}/tools/clang/tools/extra"
}

package_compiler-rt() {
  pkgdesc="Runtime libraries for Clang and LLVM (mingw-w64)"
  url="https://compiler-rt.llvm.org/"

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "${srcdir}/build-${MSYSTEM}/runtimes"
  mkdir -p "${pkgdir}${MINGW_PREFIX}/bin/"
  find "${pkgdir}${MINGW_PREFIX}/lib/clang/${pkgver%.[0-9].*}/lib/windows/" \
    -name "*.dll" -exec mv '{}' "${pkgdir}${MINGW_PREFIX}/bin/" \;
  rmdir "${pkgdir}${MINGW_PREFIX}/bin/" 2>/dev/null || true
}

package_lld() {
  pkgdesc="Linker tools for LLVM (mingw-w64)"
  url="https://lld.llvm.org/"
  groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
  depends=(
    "${MINGW_PACKAGE_PREFIX}-cc-libs"
    "${MINGW_PACKAGE_PREFIX}-llvm-libs=${pkgver}"
    "${MINGW_PACKAGE_PREFIX}-zlib"
    "${MINGW_PACKAGE_PREFIX}-zstd"
  )
  provides=("${MINGW_PACKAGE_PREFIX}-binutils")

  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "${srcdir}/build-${MSYSTEM}/tools/lld"
  install -Dm755 "${pkgdir}${MINGW_PREFIX}/bin/lld.exe" "${pkgdir}${MINGW_PREFIX}/bin/ld.exe"
}

package_llvm() {
  pkgdesc="Low Level Virtual Machine (mingw-w64)"
  depends=(
    "${MINGW_PACKAGE_PREFIX}-llvm-libs=${pkgver}"
    "${MINGW_PACKAGE_PREFIX}-llvm-tools=${pkgver}"
  )

  sed -i.orig '/tools\/cmake_install.cmake/d' "${srcdir}/build-${MSYSTEM}/cmake_install.cmake"
  sed -i.orig '/compiler-rt\/cmake_install.cmake/d' "${srcdir}/build-${MSYSTEM}/projects/cmake_install.cmake"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "${srcdir}/build-${MSYSTEM}"
  install -d "${pkgdir}${MINGW_PREFIX}/share/llvm/cmake/"{modules,platforms}
  install -Dm644 "${srcdir}/llvm/cmake/modules/"*.cmake "${pkgdir}${MINGW_PREFIX}/share/llvm/cmake/modules/"
  install -Dm644 "${srcdir}/llvm/cmake/platforms/"*.cmake "${pkgdir}${MINGW_PREFIX}/share/llvm/cmake/platforms/"
  sed -e "s|$(cygpath -wm "${MINGW_PREFIX}")|${MINGW_PREFIX}|g" -i "${pkgdir}/${MINGW_PREFIX}/lib/cmake/llvm/LLVMExports.cmake"
}

package_llvm-tools() {
  pkgdesc="Low Level Virtual Machine - Tools (mingw-w64)"
  depends=("${MINGW_PACKAGE_PREFIX}-llvm-libs=${pkgver}")

  sed -i.orig '/\(clang\|lld\)\/cmake_install.cmake/d' "${srcdir}/build-${MSYSTEM}/tools/cmake_install.cmake"
  DESTDIR="${pkgdir}" "${MINGW_PREFIX}/bin/cmake" --install "${srcdir}/build-${MSYSTEM}/tools"
  mkdir -p "${srcdir}/llvm-libs/${MINGW_PREFIX}/bin"
  mv -f "${pkgdir}${MINGW_PREFIX}/bin/"lib{LLVM-*,LTO,Remarks}.dll "${srcdir}/llvm-libs/${MINGW_PREFIX}/bin/"
  rm -r "${pkgdir}${MINGW_PREFIX}/include/llvm-c"
  cp "${pkgdir}${MINGW_PREFIX}/bin/llvm-cov.exe" "${pkgdir}${MINGW_PREFIX}/bin/gcov.exe"
}

package_llvm-libs() {
  pkgdesc="Low Level Virtual Machine Runtime Libraries (mingw-w64)"
  depends=(
    "${MINGW_PACKAGE_PREFIX}-cc-libs"
    "${MINGW_PACKAGE_PREFIX}-libffi"
    "${MINGW_PACKAGE_PREFIX}-libxml2"
    "${MINGW_PACKAGE_PREFIX}-zlib"
    "${MINGW_PACKAGE_PREFIX}-zstd"
  )

  cp -r "${srcdir}/llvm-libs/${MINGW_PREFIX}" "${pkgdir}${MINGW_PREFIX}"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
